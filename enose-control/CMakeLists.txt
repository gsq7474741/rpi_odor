cmake_minimum_required(VERSION 3.25)
project(enose-control CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# output to bin directory
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

# System dependencies
find_package(Boost REQUIRED COMPONENTS system thread date_time)
find_package(fmt REQUIRED)
find_package(spdlog REQUIRED)
find_package(nlohmann_json REQUIRED)
find_package(gRPC REQUIRED)
find_package(Protobuf REQUIRED)
find_package(yaml-cpp REQUIRED)
find_package(libpqxx REQUIRED)
find_package(PkgConfig REQUIRED)
pkg_check_modules(SYSTEMD REQUIRED libsystemd)

# Use pre-generated Protobuf & gRPC code (generated by buf)
set(PROTO_GEN_DIR "${CMAKE_SOURCE_DIR}/../gen/cpp")

# Collect all generated source files
file(GLOB PROTO_SRCS "${PROTO_GEN_DIR}/*.pb.cc" "${PROTO_GEN_DIR}/*.grpc.pb.cc")
file(GLOB PROTO_HDRS "${PROTO_GEN_DIR}/*.pb.h" "${PROTO_GEN_DIR}/*.grpc.pb.h")

# Also include buf/validate if exists
file(GLOB_RECURSE VENDOR_SRCS "${PROTO_GEN_DIR}/buf/*.pb.cc")
file(GLOB_RECURSE VENDOR_HDRS "${PROTO_GEN_DIR}/buf/*.pb.h")
list(APPEND PROTO_SRCS ${VENDOR_SRCS})
list(APPEND PROTO_HDRS ${VENDOR_HDRS})

# Create a library for the generated proto code
add_library(proto_lib STATIC ${PROTO_SRCS} ${PROTO_HDRS})
target_link_libraries(proto_lib PUBLIC protobuf::libprotobuf gRPC::grpc++)
target_include_directories(proto_lib PUBLIC ${PROTO_GEN_DIR})

# Sources
file(GLOB_RECURSE SOURCES "src/*.cpp")

add_executable(${PROJECT_NAME} ${SOURCES})

target_include_directories(${PROJECT_NAME} PRIVATE src)

# Link libraries
# libpqxx 静态链接, libpq 动态链接 (RPi5 需要: sudo apt install libpq5)
target_link_libraries(${PROJECT_NAME} PRIVATE
    Boost::system
    Boost::thread
    Boost::date_time
    fmt::fmt
    spdlog::spdlog
    nlohmann_json::nlohmann_json
    gRPC::grpc++
    protobuf::libprotobuf
    yaml-cpp
    libpqxx::pqxx
    proto_lib
    ${SYSTEMD_LIBRARIES}
)

target_include_directories(${PROJECT_NAME} PRIVATE ${SYSTEMD_INCLUDE_DIRS})

# Compile definitions
target_compile_definitions(${PROJECT_NAME} PRIVATE
    BOOST_ASIO_NO_DEPRECATED
)
